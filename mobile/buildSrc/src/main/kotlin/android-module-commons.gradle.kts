import com.android.build.api.dsl.LibraryAndroidResources
import dev.detekt.gradle.extensions.DetektExtension
import jacoco.setupJacocoMergingAndroid
import org.gradle.accessors.dm.LibrariesForLibs
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import util.commonAndroid
import util.commonAndroidComponents

val libs = the<LibrariesForLibs>()

plugins {
   id("all-modules-commons")
   id("org.gradle.android.cache-fix")
}

val customConfig = extensions.create<CustomBuildConfiguration>("custom")

commonAndroid {
   // Use default namespace for no resources, modules that use resources must override this
   // Add a unique suffix to every module to stop AGP from complaining about "is used in multiple modules"
   // Workaround for the https://issuetracker.google.com/issues/332947919
   val uniqueNamespaceSuffix = path.removePrefix(":").replace(':', '.').replace("-", "")
   namespace = "com.ltpitt.pebblin.noresources.$uniqueNamespaceSuffix"

   compileSdk = 36

   compileOptions.apply {
      sourceCompatibility = JavaVersion.VERSION_17
      targetCompatibility = JavaVersion.VERSION_21

      isCoreLibraryDesugaringEnabled = true
   }

   defaultConfig.apply {
      minSdk = 24

      testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
   }

   testOptions.apply {
      unitTests.all {
         it.useJUnitPlatform()

         // Better test output
         it.systemProperty("kotest.assertions.collection.print.size", "300")
         it.systemProperty("kotest.assertions.collection.enumerate.size", "300")
      }

      if (pluginManager.hasPlugin("com.android.library")) {
         targetSdk = 34
      }
   }

   packaging.resources.excludes += "/META-INF/{AL2.0,LGPL2.1}"

   buildFeatures.apply {
      buildConfig = false
      resValues = false
      shaders = false
   }

   val androidResources = androidResources
   if (androidResources is LibraryAndroidResources) {
      androidResources.enable = false
   }

   buildTypes.getByName("debug").apply {
      testCoverage.jacocoVersion = libs.versions.jacoco.get()
      enableUnitTestCoverage = true
      enableAndroidTestCoverage = true
   }
}

tasks.withType<KotlinCompile>().configureEach {
   compilerOptions.jvmTarget.set(JvmTarget.JVM_21)
}

project.setupJacocoMergingAndroid()

dependencies {
   add("coreLibraryDesugaring", libs.desugarJdkLibs)
   add("detektPlugins", project(":detekt"))

   if (configurations.findByName("androidTestImplementation") != null) {
      add("androidTestImplementation", libs.kotest.assertions)
   }
}

configure<DetektExtension> {
   config.from("$rootDir/config/detekt-android.yml")
}

val runDebugTestsTask = tasks.register("runDebugTests")
val runDebugDetektTask = tasks.register("runDebugDetekt")

commonAndroidComponents {
   onVariants { variant ->
      // For variants, you can add extra filters, such as
      // && (variant.productFlavors.isEmpty() || variant.productFlavors.contains("version" to "develop"))
      if (variant.buildType == "debug") {

         if (!pluginManager.hasPlugin("com.android.test")) {
            runDebugTestsTask.configure {
               dependsOn(variant.computeTaskName("test", "UnitTest"))
            }
            runDebugDetektTask.configure {
               dependsOn(variant.computeTaskName("detekt", "UnitTest"))
               dependsOn(variant.computeTaskName("detekt", "AndroidTest"))
            }
         }
         runDebugDetektTask.configure {
            dependsOn("detekt${variant.name.replaceFirstChar { it.uppercaseChar() }}")
         }
      }
   }
}

// Even empty android test tasks take a while to execute. Disable all of them by default.
@Suppress("ComplexCondition") // It is just a properly commented list of tasks
tasks.configureEach {
   if (!customConfig.enableEmulatorTests.getOrElse(false) &&
      name.contains("AndroidTest", ignoreCase = true) &&
      !javaClass.name.startsWith("com.autonomousapps") && // https://github.com/autonomousapps/dependency-analysis-gradle-plugin/issues/945
      !name.contains("Lint", ignoreCase = true) // Android lint does not like disabling their tasks
   ) {
      enabled = false
   }
}
